// Copyright 2022 agwlvssainokuni
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

plugins {
    id "java-library"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "org.flywaydb.flyway" version "8.5.12"
}

sourceCompatibility = 17
targetCompatibility = 17

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

repositories { mavenCentral() }

configurations { codegen }

test { useJUnitPlatform() }

dependencyManagement {
    imports { mavenBom "org.springframework.boot:spring-boot-dependencies:2.7.0" }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-jdbc"
    implementation "com.querydsl:querydsl-core"
    implementation "com.querydsl:querydsl-sql"
    implementation "com.querydsl:querydsl-sql-spring"
    runtimeOnly "org.flywaydb:flyway-core"
    runtimeOnly "com.h2database:h2"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    codegen "com.querydsl:querydsl-sql-codegen"
    codegen "com.h2database:h2"
}

flyway {
    url = "jdbc:h2:file:" + file("./build/codegen")
    user = "sa"
    password = ""
}

task codegen {
    dependsOn flywayMigrate
    ant.taskdef(
        name: "codegen",
        classname: "com.querydsl.sql.codegen.ant.AntMetaDataExporter",
        classpath: configurations.codegen.asPath
    )
    doLast {
        ant.codegen(
            jdbcDriver: "org.h2.Driver",
            jdbcUrl: "jdbc:h2:file:" + file("./build/codegen"),
            jdbcUser: "sa",
            jdbcPassword: "",
            targetFolder: file("src/main/java"),
            exportBeans: true,
            schemaPattern: "",
            packageName: "cherry.tutorial.querydsl.db",
            namePrefix: "Q",
            beanPrefix: "B",
            beanAddToString: true
        )
    }
}
