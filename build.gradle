// Copyright 2022 agwlvssainokuni
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

buildscript {
    repositories { mavenCentral() }
    dependencies {
        classpath "org.flywaydb:flyway-mysql:8.5.13"
    }
}

plugins {
    id "java-library"
    id "io.spring.dependency-management" version "1.0.12.RELEASE"
    id "org.flywaydb.flyway" version "8.5.13"
}

apply plugin: "querydslexport"

sourceCompatibility = 17
targetCompatibility = 17

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

repositories { mavenCentral() }

configurations { codegen }

test { useJUnitPlatform() }

dependencyManagement {
    imports { mavenBom "org.springframework.boot:spring-boot-dependencies:2.7.2" }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-jdbc"
    implementation "com.querydsl:querydsl-core"
    implementation "com.querydsl:querydsl-sql"
    implementation "com.querydsl:querydsl-sql-spring"
    runtimeOnly "org.flywaydb:flyway-core"
    runtimeOnly "org.flywaydb:flyway-mysql"
    runtimeOnly "mysql:mysql-connector-java"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    codegen "com.querydsl:querydsl-sql-codegen"
    codegen "mysql:mysql-connector-java"
}

flyway {
    url = "jdbc:mysql://127.0.0.1:3306/tutorial"
    user = "tutorial"
    password = "password"
}

querydsl {
    jdbcDriver = "com.mysql.cj.jdbc.Driver"
    jdbcUrl = "jdbc:mysql://127.0.0.1:3306/tutorial"
    jdbcUser = "tutorial"
    jdbcPassword = "password"
    targetFolder = file("src/main/java")
    packageName = "cherry.tutorial.querydsl.db"
    catalogPattern = "tutorial"
    tableNamePattern = "author, todo"
    namePrefix = "Q"
    beanPrefix = "B"
    exportBeans = true
    //exportPrimaryKeys = true
    //beanAddToString = true
    customTypes = [
        "com.querydsl.sql.types.JSR310LocalDateTimeType",
        "com.querydsl.sql.types.JSR310LocalDateType",
        "com.querydsl.sql.types.JSR310LocalTimeType"
    ]
}

task codegen {
    dependsOn flywayMigrate
    ant.taskdef(
        name: "codegen",
        classname: "com.querydsl.sql.codegen.ant.AntMetaDataExporter",
        classpath: configurations.codegen.asPath
    )
    doLast {
        ant.codegen(
            jdbcDriver: "com.mysql.cj.jdbc.Driver",
            jdbcUrl: "jdbc:mysql://127.0.0.1:3306/tutorial",
            jdbcUser: "tutorial",
            jdbcPassword: "password",
            targetFolder: file("src/main/java"),
            packageName: "cherry.tutorial.querydsl.db",
            tableNamePattern: "author, todo",
            exportBeans: true,
            beanAddToString: true,
            namePrefix: "Q",
            beanPrefix: "B"
        ) {
            customType(className: "com.querydsl.sql.types.JSR310LocalDateTimeType")
            customType(className: "com.querydsl.sql.types.JSR310LocalDateType")
            customType(className: "com.querydsl.sql.types.JSR310LocalTimeType")
        }
    }
}
